#!/usr/local/bin/perl
# -*- perl -*-

# Copyright (c) 2013
# Author: Jeff Weisberg <jaw @ solvemedia.com>
# Created: 2013-Jan-15 10:45 (EST)
# Function: 
#
# $Id$

use Text::CSV;
use Math::Trig qw(deg2rad pi great_circle_distance asin acos);

use strict;

my $OUTFILE = '/home/acdata/dns_mm_ipv4.mdb';

my $BLKFILE = '/home/bagel/u/jaw/tmp/GeoLiteCity_20130101/GeoLiteCity-Blocks.csv';
my $LOCFILE = '/home/bagel/u/jaw/tmp/GeoLiteCity_20130101/GeoLiteCity-Location.csv';


my %DC = (
    ccsphl	=> {
        lat => 39.964622,
        lon => -75.161762,
    },

    qtssjc => {
        lat => 37.392664,
        lon => -121.978455,
    },

    savchi => {
        lat => 41.854323,
        lon => -87.618531,
    },

    swiams => {
        lat => 52.304136,
        lon => 4.939256,
    },
   );

my $HEADSIZE = 8192;
my %LOC;
my $nrec = 0;

my @DC = (keys %DC);

################################################################

open(TMP, "> $OUTFILE.tmp.$$") || fatal("cannot open tmp file: $!");
binmode(TMP, ':raw');
# skip header
syswrite(TMP, "\0" x $HEADSIZE);

read_locfile();
read_blkfile();

do_header();
close TMP;

rename "$OUTFILE.tmp.$$", $OUTFILE;

################################################################

sub read_locfile {

    open(my $f, $LOCFILE);
    my $csv = Text::CSV->new({binary => 1});

    while(my $l = $csv->getline($f)){
        $LOC{ $l->[0] } = { lat => $l->[5], lon => $l->[6] };
    }
}

sub read_blkfile {

    open(my $f, $BLKFILE);
    <$f>; <$f>;

    my $csv = Text::CSV->new();

    while(my $l = $csv->getline($f)){
        my $loc = $LOC{ $l->[2] };
        print STDERR  "$l->[0] => $l->[2]\n" unless $loc;
        next unless $loc;
        my $masklen = masklen( $l->[0], $l->[1] );
        print STDERR "$l->[0] - $l->[1] => $masklen\n" if $masklen > 32 || $masklen < 8;

        my $rec = pack('NNsS',
                       $l->[0], 0,	# 64bit addr
                       $masklen,	# masklen
                       0)		# flags
          . join('', map { pack('l', pingtime($_, $DC{$_}, $loc)) } @DC);

        print TMP $rec;
        $nrec ++;
    }
}

sub masklen {
    my $a = shift;
    my $z = shift;

    if( $a == $z ){
        return 32;
    }
    int 32 - log($z - $a) / log(2);
}

sub pingtime {
    my $dc   = shift;
    my $loc1 = shift;
    my $loc2 = shift;

    my $dist = haversine( $loc1->{lat}, $loc1->{lon}, $loc2->{lat}, $loc2->{lon} );

    return $dist;
}


sub haversine {
  my ($lat1, $long1, $lat2, $long2) = @_;
  my $r = 1100;	# ~ millisecs

  my $dlong = deg2rad($long1) - deg2rad($long2);
  my $dlat  = deg2rad($lat1)  - deg2rad($lat2);

  my $a = sin($dlat/2)**2 +cos(deg2rad($lat1))
                    * cos(deg2rad($lat2))
                    * sin($dlong/2)**2;

  my $c = 2 * (asin(sqrt($a)));

  # print STDERR ">> $lat1, $long1, $lat2, $long2 => $a,$c\n";
  return $r * $c;
}

sub do_header {

    my $hsize = 128;
    my $dcs = join('', map { "$_\0" } @DC);

    my $rec = pack('LL ll qq qq',
                   0x41436d46, 1,		# magic, version
                   4, (12 + 4 * @DC),		# ipver, recsize
                   $hsize, (scalar @DC),	# dc pos, #dc
                   $HEADSIZE, $nrec,		# rec pos, #rec
                  );

    $rec .= "\0" x ($hsize - length($rec)) . $dcs;
    $rec .= "\0" x ($HEADSIZE - length($rec));

    seek TMP, 0, 0;
    syswrite(TMP, $rec);

}


